Grammar = {
    SOI ~
    Item* ~
    EOI
}

restOfLine = _{
    (LETTER | MARK | NUMBER | PUNCTUATION | SYMBOL | SPACE_SEPARATOR)*
}

Item = _{
    ((Comment | Declaration) ~ (NEWLINE | EOI))
    | EmptyLine
    | Rule
}

Comment = { "#" ~ restOfLine }

EmptyLine = @{ " "* ~ "\n" }

Declaration = { VariableName ~ " "* ~ ":=" ~ " "* ~ VariableContent ~ " "* }
VariableName = {
    (ASCII_ALPHA | "_" ) ~
    (ASCII_ALPHANUMERIC | "_" )+
}
VariableContent = { restOfLine }

Rule = {
    RuleTargets ~ ":" ~ " "* ~ RuleDependencies
    ~ (NEWLINE | EOI)
    ~ RuleRecipe
}
RuleTargets = {
    ".PHONY" | (
        VariableName ~
        (" "+ ~ VariableName)*
    )
}
RuleDependencies = {
    VariableContent?
}
RuleRecipe = {
    ("\t" ~ RuleRecipeLine ~ (NEWLINE | EOI))*
}
RuleRecipeLine = {
    RuleRecipeNonStatement ~
    ( (EvaluatedStatement | LiteralDollar) ~ RuleRecipeNonStatement)*
}
RuleRecipeNonStatement = {
    (!"$" ~ ANY)*
}
EvaluatedStatement = {
    "$(" ~ EvaluatedStatementContent ~ ")"
}

LiteralDollar = { "$$" }

// for now, keep it simple: just a variable name.
// later, we might allow function calls and recursive variables
EvaluatedStatementContent = { VariableName }